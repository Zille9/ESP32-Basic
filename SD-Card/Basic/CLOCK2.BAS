5 COL 1,7
6 FONT0
10 CLS:CUR 0
20 XV=159
30 YV=119
35 RV=95
40 W=220
42 ML=80:HL=60
44 SL=88
45 PEN0,0
47 COL 0,56
50 CIRC XV,YV,W,W,1
52 CIRC XV,YV,W,W,0
54 CIRC XV,YV,W-8,W-8,0
60 FOR I=0 TO 12
70 X=XV+RV*COS(ANGLE*PI()/180)
80 Y=YV+RV*SIN(ANGLE*PI()/180)
82 COL 0,1
90 CIRC X,Y,4,4,1
100 ANGLE=ANGLE+30
110 NEXT I
120 FOR I=0 TO 360 STEP 6
130 PSET XV+RV*COS(I*PI()/180),YV+RV*SIN(I*PI()/180)
140 NEXT I
150 REM ----- DATUM -----
160 PEN 47
170 POS 16,10
180 D=DATE(0):MO=DATE(1):J=DATE(2)
190 IF D<10 THEN PRINT"0";
200 PRINT D;".";
210 IF MO<10 THEN PRINT"0";
220 PRINT MO;".";
230 PRINT J;
240 GOSUB 1000
270 REM ----- STUNDENZEIGER -----
280 H=TIME(0):M=TIME(1):S=TIME(2)
290 ANGLE=M*0.1044-1.571
300 X=XV+ML*COS(ANGLE)
310 Y=YV+ML*SIN(ANGLE)
320 PEN 47
330 LINE XV,YV,X,Y
340 REM ----- MINUTENZEIGER -----
350 ANGLE=H*0.524+M*0.0087-1.571
360 X=XV+HL*COS(ANGLE)
370 Y=YV+HL*SIN(ANGLE)
380 PEN61
390 LINE XV,YV,X,Y
400 REM ----- SEKUNDENZEIGER -----
410 ANGLE=S*0.1044-1.566
420 X=XV+SL*COS(ANGLE)
430 Y=YV+SL*SIN(ANGLE)
440 PEN 1
450 LINE XV,YV,X,Y
460 COL 63,0
470 CIRC XV,YV,15,15,1
480 COL 63,36
490 IF TIME(2)=S THEN GOTO 490
500 CIRC XV,YV-1,W-40,W-40,1
510 GOTO 150
1000 DO=DATE(3)
1010 IF DO=1 THEN A$="Montag"
1020 IF DO=2 THEN A$="Dienstag"
1030 IF DO=3 THEN A$="Mittwoch"
1040 IF DO=4 THEN A$="Donnerstag"
1050 IF DO=5 THEN A$="Freitag"
1060 IF DO=6 THEN A$="Samstag"
1070 IF DO=0 THEN A$="Sonntag"
1080 POS16+ABS(LEN(A$)-10)/2,22
1090 PRINT A$;
1100 RETURN
